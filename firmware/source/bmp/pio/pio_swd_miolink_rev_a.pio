; This file contains the programs responsible for the SWD interface functionality for native MioLink revA board.
; The SWD program is divided into sub-programs for more convenient addressing in C code,
; origin directive allows modifying the PIO program without affecting the C code.
; 7 sub-programs are implemented here:

; miolink_rev_a_swd_start - start of the program, just wait for the next command;
; miolink_rev_a_swd_seq_out_turnaround - turnaround from input to output before out sequence;
; miolink_rev_a_swd_seq_out - TMS out sequence;
; miolink_rev_a_swd_seq_in_turnaround - turnaround from output to input before in sequence;
; miolink_rev_a_swd_seq_in - TMS in sequence;
; miolink_rev_a_swd_adiv5_check_ack - check ADIv5 acknowledge and discard TX FIFO content if ACK is incorrect;
; miolink_rev_a_swd_turnaround_float_to_drive - separate turnaround from input to output without sequence after.

; Pin mapping for all programs:
; IN - TMS
; OUT - TMS
; Side-set - TCK
; Set - pin 0 is TMS (to change direction through set pindirs),
;       pin 1 is TMS_DIR (to change the external level shifter direction through set pins).

; Additional 3 delay cycles reduce the time spent on non-data instructions, but at the same time
; limit the maximum interface frequency. In our case (3 additional delay cycles after each edge),
; the limit is set to sys_clk / 8, as 3 + 1 + 3 + 1 = 8.

; Autopull should be enabled, with a threshold of 32.
; Autopush also should be enabled, with a threshold of 32.


; Program start
.program miolink_rev_a_swd_start
.pio_version 0
.origin 0
.side_set 1 opt

out pc, 32                                  ; wait for the next command

; TMS turnaround from input to output state
.program miolink_rev_a_swd_seq_out_turnaround
.pio_version 0
.origin 1
.side_set 1 opt

set pins, 2 side 1    [3]                   ; set the TMS_DIR pin to 1 (2 = 0b10, level shifter - output) and a rising edge on TCK
set pindirs, 3 side 0 [2]                   ; set TMS_DIR and TMS pindirs to outputs (3 = 0b11) and a falling edge on TCK


; TMS sequence out
.program miolink_rev_a_swd_seq_out
.pio_version 0
.origin 3
.side_set 1 opt
                                            ; TMS direction should be output and TMS_DIR state should be 1
out x, 32                                   ; load the number of cycles from TX FIFO
swd_seq_out_loop:
    out pins, 1 side 0 [3]                  ; output TMS pin state and a falling edge on TCK
    jmp x-- swd_seq_out_loop side 1 [3]     ; decrement the cycle counter and a rising edge on TCK

pull block side 0 [2]                       ; flush remaining data in osr and set falling edge on TCK
out pc, 32                                  ; wait for next command


; TMS turnaround from output to input state
.program miolink_rev_a_swd_seq_in_turnaround
.pio_version 0
.origin 8
.side_set 1 opt

set pindirs, 2 side 1 [3]                   ; set the TMS_DIR pindir to output, TMS pindir to input (2 = 0b10)
                                            ; and a rising edge on TCK
set pins, 0 side 0    [2]                   ; set the TMS_DIR pin to 0 (level shifter - input) and a falling edge on TCK


; TMS sequence input
.program miolink_rev_a_swd_seq_in
.pio_version 0
.origin 10
.side_set 1 opt
                                            ; TMS direction should be input, and TMS_DIR state should be 0
    out x, 32                               ; load number of cycles from TX FIFO
    swd_seq_in_loop:
        in pins, 1 side 0 [3]               ; read TMS pin state into ISR and a falling edge on TCK
        jmp x-- swd_seq_in_loop side 1 [3]  ; decrement the cycle counter and a rising edge on TCK

    mov x, isr side 0 [1]                   ; save ISR content to X register to check the ACK later
                                            ; and a falling edge on TCK
    push                                    ; push isr content to RX FIFO for the case when cycle counter < 32 (autopush threshold)
    out pc, 32                              ; wait for next command


; Check ADIv5 acknowledge
.program miolink_rev_a_swd_adiv5_check_ack
.pio_version 0
.origin 16
.side_set 1 opt

out y, 32                                   ; load ack value in Y register

jmp x!=y clear_tx_fifo                      ; if the values don't match - go to the clear TX FIFO step
out null, 32                                ; if the values match - discard the number of data to clear
out pc, 32                                  ; wait for the next command

clear_tx_fifo:
    out x, 32                               ; load the number of data to discard

    clear_tx_fifo_loop:
        out null, 32                        ; discard TX FIFO content
        jmp x-- clear_tx_fifo_loop          ; decrement data counter
                                            ; Intentional fallthrough to turnaround


; Turnaround from input to output state
.program miolink_rev_a_swd_turnaround_float_to_drive
.pio_version 0
.origin 23
.side_set 1 opt

set pins, 2 side 1 [3]                      ; set the TMS_DIR pin to 1 (2 = 0b10, level shifter - output) and a rising edge on TCK
set pindirs, 3 side 0 [2]                   ; set TMS_DIR and TMS pindirs to outputs (3 = 0b11) and a falling edge on TCK
                                            ; wrap to the program start